dnl Process this file with autoconf to produce a configure script.

dnl This is required at the start; the name is the name of a file
dnl it should be seeing, to verify it is in the same directory.

AC_INIT(dftables.c)

dnl Arrange to build config.h from config.in. Note that pcre.h is
dnl built differently, as it is just a "substitution" file.
dnl Manual says this macro should come right after AC_INIT.
AC_CONFIG_HEADER(config.h:config.in)

dnl Provide the current PCRE version information. Do not use numbers
dnl with leading zeros for the minor version, as they end up in a C
dnl macro, and may be treated as octal constants. Stick to single
dnl digits for minor numbers less than 10. There are unlikely to be
dnl that many releases anyway.

PCRE_MAJOR=3
PCRE_MINOR=1
PCRE_DATE=09-Feb-2000
PCRE_VERSION=${PCRE_MAJOR}.${PCRE_MINOR}

dnl Provide versioning information for libtool shared libraries that
dnl are built by default on Unix systems.

PCRE_LIB_VERSION=0:0:0
PCRE_POSIXLIB_VERSION=0:0:0

dnl Checks for programs.

AC_PROG_CC
dnl OS/390 doesn't have ranlib and its make utility thinks RANLIB=:
dnl is a rule, so set RANLIB to true if there is no ranlib.
AC_CHECK_PROG(RANLIB, ranlib, ranlib, true)

dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h)

dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.

AC_CHECK_FUNCS(memmove strerror)

dnl Handle --enable-shared-libraries

LIBTOOL=
LIBSUFFIX=a
AC_ARG_ENABLE(shared,
[  --enable-shared        build PCRE as a shared library],
if test "$enableval" = "yes"; then
  LIBTOOL=libtool
  LIBSUFFIX=la
fi
)

PLATFORM=`uname -s`

case "$PLATFORM" in
*OS/2*)
  CFLAGS="$CFLAGS -Zexe"
  ;;
esac

dnl "Export" these variables

AC_SUBST(HAVE_MEMMOVE)
AC_SUBST(HAVE_STRERROR)
AC_SUBST(LIBTOOL)
AC_SUBST(LIBSUFFIX)
AC_SUBST(PCRE_MAJOR)
AC_SUBST(PCRE_MINOR)
AC_SUBST(PCRE_DATE)
AC_SUBST(PCRE_VERSION)
AC_SUBST(PCRE_LIB_VERSION)
AC_SUBST(PCRE_POSIXLIB_VERSION)

dnl This must be last; it determines what files are written
AC_OUTPUT(Makefile pcre.h:pcre.in pcre-config,[chmod a+x pcre-config])
